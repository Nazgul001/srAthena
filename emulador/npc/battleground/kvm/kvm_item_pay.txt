//===== rAthena Script ======================================= 
//= BattleGround System - KvM Item Dealer
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena 1.0
//===== Description: ========================================= 
//= [Official Conversion]
//= Kreiger Von Midgard Battleground Point redemption NPC
//= * NOTE * 
//= KvM Does NOT use items, but a character variable to
//= keep track of points. This NPC when talked to will
//= convert already-given tokens and convert them to points.
//= under the player variable kvm_point.
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//============================================================
//===== Traduccion al Español ================================
//= 1.0 Primera Traduccion [Nazgul001]
//============================================================

bat_room,151,144,3	script	KVM Logistic Officer#a	734,{
	if (countitem(7773)) {
		set .@pointstoadd,countitem(7773);
		delitem 7773,.@pointstoadd;
		set kvm_point,kvm_point+.@pointstoadd;
		mes "[Logistics]";
		mes "Esos son"+getitemname(7773)+"s ya veo?";
		mes "Nosotros ya no aceptamos ese medio de pago.";
		mes "pero podria cambiartelos,";
		mes "Asi que tienes"+.@pointstoadd+" "+getitemname(7773)+"s?";
		mes "Esta bien, todo puesto, ahora tienes ^580080"+ kvm_point +"^000000 KVM Puntos.";
		next;
	}
	if (checkweight(1201,1) == 0) {
		mes "- Espera un minuto !! -";
		mes "- Estas cargando -";
		mes "- demasiados items contigo. -";
		mes "- Por favor intentalo -";
		mes "- despues de haber dejado algunos items en tu almacen. -";
		close;
	}
	mes "[Logistics]";
	mes "Hola?";
	mes "Yo estoy a cargo de la distribucion";
	mes "de los items de recompensa por puntos KVM.";
	mes "No devolveremos nada por";
	mes "equivocarse al escojer un item.";
	mes "Selecciona un item con precausion en el siguiete menu.";
	next;
	set .@name$,strcharinfo(0);
	switch(select("Ver el catalogo KVM.:Comprar Items KVM.:Confirmar puntos KVM.:Que son las recompensas KVM?:Que son los puntos KVM?")) {
	case 1:
		mes "[Logistics]";
		mes "Aqui esta el catalogo de los items KVM.";
		mes "Cada arma requiere el pago de  2,000 puntos";
		mes "cada armadura puede costar de 10 a 1,200";
		mes "puntos, asi que, se cuidadoso al";
		mes "elejir un item de recompensa.";
		close2;
		ReadBook 11017,1;
		end;
	case 2:
		mes "[Logistics]";
		mes "Cual item deseas ver?";
		mes "para ver la informacion detallada del";
		mes "item, por favor revisa el";
		mes "^3131FFCatalogo^000000.";
		next;
		switch(select("Weapon:Armor/Accessory:Mass-Production Armor/Accessory:Popularized Armor/Accessory")) {
		case 1:
			mes "[Logistics]";
			mes "Haz seleccionado el Catalago de Armas.";
			mes "Por favor elige una subcategoria.";
			next;
			switch(select("Dagger/Sword/Spear:Staff/Mace/Axe/Shuriken:Bow/Katar/Instrument/Whip:Book/Knuckle:Revolver/Rifle/Gun/Grenade Launcher")) {
			case 1:
				mes "[Logistics]";
				mes "Haz seleccionado Dagas/Espadas/Lanzas.";
				mes "Por favor elige una subcategoria.";
				next;
				switch(select("Dagger:One-handed Sword:Two-handed Sword:One-handed Spear:Two-handed Spear")) {
				case 1: callsub PurchaseItem,13042,0,1; //Krieger_Dagger1 
				case 2:
					mes "[Logistics]";
					mes "Haz seleccionado la categoria de Espadas de 1 mano.";
					mes "Hay 3 tipos de Espadas de 1 mano: Glorious Flamberge, Glorious Rapier y Glorious Holy Avenger.";
					mes "Si quieres ver la informacion detalla, revisa el Catalgo KVM";
					mes "Por favor selecciona un de ellas.";
					next;
					switch(select("Glorious Flamberge:Glorious Rapier:Glorious Holy Avenger")) {
					case 1: callsub PurchaseItem,13416,0,0; //Krieger_Onehand_Sword1
					case 2: callsub PurchaseItem,13417,0,0; //Krieger_Onehand_Sword2
					case 3: callsub PurchaseItem,13418,0,0; //Krieger_Onehand_Sword3
					}
				case 3: callsub PurchaseItem,1187,0,1; //Krieger_Twohand_Sword1
				case 4: callsub PurchaseItem,1426,0,1; //Krieger_Onehand_Spear1
				case 5: callsub PurchaseItem,1486,0,1; //Krieger_Twohand_Spear1
				}
			case 2:
				mes "[Logistics]";
				mes "Haz seleccionado Baculos/Mazos/Hachas/Shuriken.";
				mes "Por favor elige una subcategoria.";
				next;
				switch(select("Staff:Mace:One-handed Axe:Two-handed Axe:Shuriken")) {
				case 1:
					mes "[Logistics]";
					mes "Haz seleccionado la categoria de Baculos.";
					mes "Hay 3 tipos de baculos: Two-handed Weapon Glorious Destruction Staff, One-handed Glorious Arc Wand y Glorious Healing Staff.";
					mes "Si quieres ver la informacion detalla, revisa el Catalgo KVM";
					mes "Por favor selecciona un de ellas.";
					next;
					switch(select("Glorious Destruction Staff:Glorious Arc Wand:Glorious Healing Staff")) {
					case 1: callsub PurchaseItem,2002,0,0; //Krieger_Twohand_Staff1
					case 2: callsub PurchaseItem,1640,0,0; //Krieger_Onehand_Staff1
					case 3: callsub PurchaseItem,1641,0,0; //Krieger_Onehand_Staff2
					}
				case 2: callsub PurchaseItem,1546,0,1; //Krieger_Onehand_Mace1
				case 3: callsub PurchaseItem,1310,0,1; //Krieger_Onehand_Axe1
				case 4: callsub PurchaseItem,1382,0,1; //Krieger_Twohand_Axe1
				case 5: callsub PurchaseItem,13307,0,1; //Krieger_Huuma_Shuriken1
				}
			case 3:
				mes "[Logistics]";
				mes "Haz seleccionado la categoria de Arcos/Katares/Instrumentos/Latigos.";
				mes "Por favor elige una subcategoria.";
				next;
				switch(select("Bow:Katar:Instrument:Whip")) {
				case 1: callsub PurchaseItem,1743,0,1; //Krieger_Bow1
				case 2:
					mes "[Logistics]";
					mes "Haz seleccionado la categoria de Katares.";
					mes "Hay 2 tipos de Katares: Glorious Bloody Roar y Glorious Jamadhar.";
					mes "Si quieres ver la informacion detalla, revisa el Catalgo KVM";
					mes "Por favor selecciona un de ellas.";
					next;
					switch(select("Glorious Bloody Roar:Glorious Jamadhar")) {
					case 1: callsub PurchaseItem,1281,0,0; //Krieger_Katar1
					case 2: callsub PurchaseItem,1282,0,0; //Krieger_Katar2
					}
				case 3: callsub PurchaseItem,1927,0,1; //Krieger_Instrument1
				case 4: callsub PurchaseItem,1981,0,1; //Krieger_Whip1
				}
			case 4:
				mes "[Logistics]";
				mes "Haz seleccionado la categoria de Libros/Empunaduras.";
				mes "Por favor elige una subcategoria.";
				next;
				switch(select("Book:Knuckle")) {
				case 1:
					mes "[Logistics]";
					mes "Haz seleccionado la categoria de Libros.";
					mes "Hay 2 tipos de Libros: Glorious Tablet y Glorious Apocalypse.";
					mes "Si quieres ver la informacion detalla, revisa el Catalgo KVM";
					mes "Por favor selecciona un de ellas.";
					next;
					switch(select("Glorious Tablet:Glorious Apocalypse")) {
					case 1: callsub PurchaseItem,1576,0,0; //Krieger_Book1
					case 2: callsub PurchaseItem,1577,0,0; //Krieger_Book2
					}
				case 2:
					mes "[Logistics]";
					mes "Haz seleccionado la categoria de Empunaduras.";
					mes "Hay 2 tipos de Empunaduras: Glorious Claw y Glorious Fist.";
					mes "Si quieres ver la informacion detalla, revisa el Catalgo KVM";
					mes "Por favor selecciona un de ellas.";
					next;
					switch(select("Glorious Claw:Glorious Fist")) {
					case 1: callsub PurchaseItem,1826,0,0; //Krieger_Knuckle1
					case 2: callsub PurchaseItem,1827,0,0; //Krieger_Knuckle2
					}
				}
			case 5:
				mes "[Logistics]";
				mes "Haz seleccionado la categoria de Revolver/Rifle/Pistola/Lanzador de Granadas.";
				mes "Por favor elige una subcategoria.";
				next;
				switch(select("Revolver:Rifle:Gatling Gun:Shotgun:Grenade Launcher")) {
				case 1: callsub PurchaseItem,13110,0,1; //Krieger_Pistol1
				case 2: callsub PurchaseItem,13176,0,1; //Krieger_Rifle1
				case 3: callsub PurchaseItem,13177,0,1; //Krieger_Gatling1
				case 4: callsub PurchaseItem,13178,0,1; //Krieger_Shotgun1
				case 5: callsub PurchaseItem,13179,0,1; //Krieger_Launcher1
				}
			}
		case 2:
			mes "[Logistics]";
			mes "Haz seleccionado la categoria de Armaduras/Accesorios.";
			mes "Por favor elige una subcategoria.";
			next;
			switch(select("Armor:Cloak:Shoes:Accessory")) {
			case 1: callsub PurchaseItem,2394,1,2; //Krieger_Suit1
			case 2: callsub PurchaseItem,2549,2,2; //Krieger_Muffler1
			case 3: callsub PurchaseItem,2444,3,2; //Krieger_Shoes1
			case 4: callsub PurchaseItem,2772,4,2; //Krieger_Ring1
			}
		case 3:
			mes "[Logistics]";
			mes "Haz seleccionado la categoria de 'Mass-Production Armor/Accessory'.";
			mes "Por favor elige una subcategoria.";
			next;
			switch(select("Mass-Production Armor:Mass-Production Shoes:Mass-Production Accessory")) {
			case 1: callsub PurchaseItem,2395,5,3; //Krieger_Suit2
			case 2: callsub PurchaseItem,2445,6,3; //Krieger_Shoes2
			case 3: callsub PurchaseItem,2773,7,3; //Krieger_Ring2
			}
		case 4:
			mes "[Logistics]";
			mes "Haz seleccionado la categoria de 'Popularized Armor/Accessory'.";
			mes "Por favor elige una subcategoria.";
			next;
			switch(select("Popularized Armor:Popularized Shoes:Popularized Accessory")) {
			case 1: callsub PurchaseItem,2396,8,4; //Krieger_Suit3
			case 2: callsub PurchaseItem,2446,9,4; //Krieger_Shoes3
			case 3: callsub PurchaseItem,2774,10,4; //Krieger_Ring3
			}
		}
	case 3:
		mes "[Logistics]";
		mes ""+ .@name$ +", tienes ^580080"+ kvm_point +"^000000 KVM Puntos.";
		close;
	case 4:
		mes "[Logistics]";
		mes "Hay 3 categorias en las recompensas KVM: Weapon/Armor.Accessory.";
		next;
		mes "[Logistics]";
		mes "Las recompensas en armas cuentan 2,000 puntos KVM cada una.";
		mes "Hay 3 tipos de recompensas en armadura y esas cuestan 840/630/580 puntos cada una.";
		mes "La recompensa en Accesorios cuesta 1,200 KVM puntos cada uno.";
		next;
		mes "[Logistics]";
		mes "Las armas Keriger son de Nivel 4, puedes subirlas de nivel y obtendran algunos efectos especiales; Slaughter, Destruction y Blessing.";
		next;
		mes "[Logistics]";
		mes "Cuando puedas usar las 3 armaduras, tu tendras exelentes opciones para hacer set.";
		mes "Incluso, cuando un accerorio Kreiger va junto con las medallas te dara una recompensa especial.";
		next;
		mes "[Logistics]";
		mes "Finalmente, Estas son Recompensas de Mass-Production Armor y Accesorios";
		mes "Los jugadores con un nivel de 60+ pueden equiparse los equipos de Mass-Production, asi como tambien hay niveles limites para los equipos Popularized.";
		close;
	case 5:
		mes "[Logistics]";
		mes "Los puntos KVM son recompensas por ganar, perder o quedar en empate durante las batallas Kreiger Von Midgard (KVM).";
		mes "Tu puedes obtener items especiales por esos puntos.";
		close;
	}

PurchaseItem:
	// Pricing for Weapon, Suit1, Muffler1, Shoes1, Ring1m Suit2, Shoes2, Ring2, Suit3, Shoes3, Ring3
	setarray .@prices[0],2000,840,630,580,1200,120,70,200,15,10,30;

	mes "[Logistics]";

	if (!getarg(2))
		mes "Haz seleccionado ^ff0000"+getitemname(getarg(0))+"^000000.";
	else if (getarg(2) == 1)
		mes "Solo hay una "+callfunc("F_GetWeaponType",getarg(0))+": "+getitemname(getarg(0))+".";
	else if (getarg(2) == 2) {
		mes "Haz seleccionado "+callfunc("F_GetArmorType",getarg(0))+"'.";
		mes "La armadura de aquel que su nivel sea superior a 80 es: ^ff0000"+getitemname(getarg(0))+"^000000.";
	}
	else if (getarg(2) == 3) {
		mes "Haz seleccionado 'Mass-Production "+callfunc("F_GetArmorType",getarg(0))+"'.";
		mes "Solo hay una Mass-Production "+callfunc("F_GetArmorType",getarg(0))+": ^ff0000"+getitemname(getarg(0))+"^000000.";
	}
	else if (getarg(2) == 4) {
		mes "Haz selecionado 'Popularized "+callfunc("F_GetArmorType",getarg(0))+"'.";
		mes "Solo hay una Popularized "+callfunc("F_GetArmorType",getarg(0))+": ^ff0000"+getitemname(getarg(0))+"^000000.";
	}

	if (!getarg(1))
		mes "Esto requiere ^0000ff2,000^000000 Puntos KVM , y "+ .@name$ +", tus puntos son: ^580080"+ kvm_point +"^000000.";
	else if (getarg(1) == 4)
		mes "Esto requiere ^0000ff2,000^000000 Puntos KVM , y "+ .@name$ +", tus puntos son: ^580080"+ kvm_point +"^000000.";
	else
		mes "Esto requiere ^0000ff"+.@prices[getarg(1)]+"^000000 Puntos KVM, y "+ .@name$ +", tus puntos son: ^580080"+ kvm_point +"^000000.";

	mes "¿Seguro de que deseas comprar este item?";
	next;
	switch(select("No, no deseo comprarlo.:Si, deseo comprarlo.")) {
	case 1:
		mes "[Logistics]";
		mes "Seleccionaste, No quiero comprar este item.";
		mes "Cuando vayas a comprar un item ten cuidado, no regresamos los puntos una vez la compra este hecha";
		break;
	case 2:
		if (kvm_point >= .@prices[getarg(1)]) {
			set kvm_point,kvm_point-.@prices[getarg(1)];
			getitem getarg(0),1;
			mes "[Logistics]";
			mes "Haz comprado una"+getitemname(getarg(0))+".";
			if (!getarg(1))
				mes "Tus puntos se redujeron a ^0000ff"+getarg(1)+"^000000 puntos, Tus puntos KVM actuales son : ^580080"+kvm_point+"^000000.";
			if (getarg(1) == 4)
				mes "Tus puntos se redujeron a ^0000ff1,200^000000 puntos, Tus puntos KVM son ^580080"+kvm_point+"^000000.";
			else
				mes "Tus puntos se redujeron a ^0000ff"+.@prices[getarg(1)]+"^000000 puntos, Tus puntos KVM actuales son : ^580080"+kvm_point+"^000000.";
		}
		else {
			mes "[Logistics]";
			if (!getarg(1))
				mes "Necesitas ^0000ff2,000^000000 puntos KVM para comprar este item.";
			else if (getarg(1) == 4)
				mes "Necesitas ^0000ff1,200^000000 puntos KVM para comprar este item.";
			else
				mes "Necesitas ^0000ff"+.@prices[getarg(1)]+"^000000 puntos KVM para comprar este item.";
			mes "Sin embargo, Tus puntos KVM ahroa son  ^580080"+ kvm_point +"^000000.";
			mes "Que no son suficientes para comprarlo.";
			mes "Cuando tengas puntos suficientes, regresa conmigo.";
		}
	}
	close;
}
