//===== rAthena Script ======================================= 
//= BattleGround System - Common NPCs
//===== By: ================================================== 
//= ????, L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena 1.0
//===== Description: ========================================= 
//= [Official Conversion]
//= Battleground NPCs:
//= - Generals and Aides
//= - Battleground Warper
//= - Kafra and Repairman.
//= - GM Management NPC (disabled by default)
//= - Badge Exchanger (Tierra and Flavius)
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Updated several NPCs to Official.
//= 1.2 Updated Repairman NPC
//= 1.3 Optimized "Erundek" NPC. [Euphy]
//============================================================
//===== Traduccion al Español ================================
//= 1.0 Primera Traduccion [Nazgul001]
//============================================================

// Generals
//============================================================
bat_room,161,158,3	script	Gen. Guillaume's Aide#01	419,{ end; }
bat_room,161,160,3	script	Gen. Guillaume's Aide#03	419,{ end; }

bat_room,160,141,3	script	Prince Croix	416,{
	cutin "bat_crua1",2;
	mes "[Prince Croix]";
	mes "Hola aventurero, nos prestarias tu poder para la victoria en la batalla?";
	next;
	switch(select("Porque es esta batalla?:Hablame sobre el General Guillaume")) {
	case 1:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "El gran Rey de Maroll, Marcel Marollo VII, ah estado muy enfermo ultimamente.";
		mes "Su majestad ah declarado que dejara el futuro de Maroll a mi o a el 3er Principe, El General Guillaume.";
		next;
		mes "[Prince Croix]";
		mes "General Guillaume ah tenido la ventaja en la batalla desde que es el Gran General de Maroll, pero eso no significa que el pueda ganar automaticamente.";
		mes "Yo deseo la victoria para traer la Paz y Prosperidad a el pueblo de Maroll. Ellos han sufrido mucho con esta guerra...";
		next;
		switch(select("Si, deseo unirme a ti.:Terminar conversasion")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "Muchisimas gracias. Siento que podemos ganar la batalla con aventureros como tu que esten dispuesto a ayudar.";
			mes "Ahora, por favor ve escaleras abajo y unete a tus camaradas de armas para prepararnos para la pelea con el enemigo.";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "Victoria por Maroll!!!!";
			break;
		}
		break;
	case 2:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "El 3er principe Guillaume es el Gran General de Maroll.";
		mes "Es una perdida de tiempo el explicarte lo gran lider militar que es, desde que comenzo el gran poder militar de Marollo.";
		next;
		mes "[Prince Croix]";
		mes "Por desgracia, hay algo que él y sus seguidores no son conscientes de:";
		mes "¿La gente de Maroll realmente quieren que gasten tanto dinero en el poder militar?";
		mes "Hemos sufrido ya muchas guerras.";
		mes "Yo no creo que las armas traigan prosperidad a la nacion.";
		next;
		mes "[Prince Croix]";
		mes "No me gusta regrar sangre, pero no tengo otra opcion mas que luchar por la posibilidad de la paz por el bien de las personas.";
		next;
		switch(select("Si, deseo unirme a ti.:Terminar conversasion")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "Muchisimas gracias. Siento que podemos ganar la batalla con aventureros como tu que esten dispuesto a ayudar.";
			mes "Ahora, por favor ve escaleras abajo y unete a tus camaradas de armas para prepararnos para la pelea con el enemigo.";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "Victoria por Maroll!!!!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_crua1",255;
	cutin "bat_crua2",255;
	end;
}

bat_room,161,140,3	script	Prince Croix's Aide#01	415,{ end; }
bat_room,161,142,3	script	Prince Croix's Aide#02	415,{ end; }

bat_room,160,159,3	script	General Guillaume	420,{
	cutin "bat_kiyom2",2;
	mes "[General Guillaume]";
	mes "Aventurero de sangre ardiente, necesitamos tus habilidades para ganar esta batalla.";
	next;
	switch(select("Porque es esta batalla?:Hablame sobre el Principe Croix")) {
	case 1:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "El gran Rey de Maroll, Marcel Marollo VII, ah estado muy enfermo ultimamente.";
		mes "Su majestad ah declarado que me escojera a mi o al Principe Croix como futuro rey de entre sus 9 hijos.";
		next;
		mes "[General Guillaume]";
		mes "Dos Reyes no pueden compartir una nacion! Solo el victorioso puede obtener el trono, el mas fuerte es quien guiara al pueblo.";
		next;
		mes "[General Guillaume]";
		mes "Esto no solo es una batalla entre nosotros, si no tambien esta batalla es por el futuro del pais.";
		mes "Me comprometo por mi honor a demostrar que yo soy quien puede proteger a Maroll de amenazas externas.";
		next;
		switch(select("Si, deseo unirme a ti.:Terminar conversasion")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Bienvenido a mi armada, camarada.";
			mes "Tus ojos me dicen que eres un soldado en el que puedo confiar.";
			next;
			mes "[General Guillaume]";
			mes "Ahora ve arriba y aplica para la batalla con tus camaradas.";
			mes "Se que te dara un gran bienvenida desde el fondo de su bravo corazon.";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "Seremos quienes capturemos la bandera!";
			break;
		}
		break;
	case 2:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "El 5to Principe Croix es el possedor del titulo de Primer Ministro de Maroll.";
		mes "El cree que el destino de una nacion puede ser discutido y determinado desde un escritorio,";
		mes "y cre que puede haber una co-existencia pacifica con otros paises.";
		next;
		mes "[General Guillaume]";
		mes "El es demasiado ignorante para admitir que la llamada paz se construye en un sinnúmero de vidas que se sacrificaron en la guerra mientras que los ciudadanos normales y las clases altas pueden vivir, ajenos a los horrores que les permitan vivir de esa manera.";
		next;
		mes "[General Guillaume]";
		mes "Es demasiado ingenuo para comprender la realidad ....";
		mes "No puedo dejar Maroll a alguien como él, que vive en un sueño!";
		next;
		mes "[General Guillaume]";
		mes "Sus creencias no realistas ahogaran este pais en la pobreza y crea al pueblo debil. Si se convierte en el rey, Maroll nunca descansaran de los ataques de otros paises.";
		mes "Quiero enseñarle lo que hace que este pequeño país sea tan poderoso y próspero. Es el poder militar!";
		next;
		switch(select("Si, deseo unirme a ti.:Terminar conversasion")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Bienvenido a mi armada, camarada.";
			mes "Tus ojos me dicen que eres un soldado en el que puedo confiar.";
			next;
			mes "[General Guillaume]";
			mes "Ahora ve arriba y aplica para la batalla con tus camaradas.";
			mes "Se que te dara un gran bienvenida desde el fondo de su bravo corazon.";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "Seremos quienes capturemos la bandera!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_kiyom1",255;
	cutin "bat_kiyom2",255;
	end;
}

// Flags
//============================================================
//bat_room,140,160,3	script	Guillaume Base#roomflag1	973,{ end; }
//bat_room,167,160,3	script	Guillaume Base#roomflag2	973,{ end; }
//bat_room,140,139,3	script	Croix Base#roomflag1	974,{ end; }
//bat_room,167,139,3	script	Croix Base#roomflag2	974,{ end; }

// BattleGround Warper
//============================================================
bat_room,148,150,5	script	Teleporter#Battlefield	124,{
	mes "[Teleporter]";
	mes "¿Quiere dejar el campo de batalla? Utilice mis servicios para volver a la ciudad.";
	next;
	switch(select("Dejar:No dejar")) {
	case 1:
		mes "[Teleporter]";
		switch(bat_return) {
		default:
		case 1:
			setarray .@mapname$[0],"Prontera.","prontera";
			setarray .@xy[0],116,72;
			break;
		case 2:
			setarray .@mapname$[0],"Morroc","moc_ruins";
			setarray .@xy[0],152,48;
			break;
		case 3:
			setarray .@mapname$[0],"Al De Baran.","aldebaran";
			setarray .@xy[0],168,112;
			break;
		case 4:
			setarray .@mapname$[0],"Geffen.","geffen";
			setarray .@xy[0],120,39;
			break;
		case 5:
			setarray .@mapname$[0],"Payon.","payon";
			setarray .@xy[0],161,58;
			break;
		case 6:
			setarray .@mapname$[0],"Lighthalzen.","lighthalzen";
			setarray .@xy[0],159,93;
			break;
		case 7:
			setarray .@mapname$[0],"Rachel.","rachel";
			setarray .@xy[0],115,124;
			break;
		}
		mes "Se le enviara de vuelta a"+.@mapname$[0]+".";
		close2;
		warp .@mapname$[1],.@xy[0],.@xy[1];
		break;
	case 2:
		mes "[Teleporter]";
		mes "Estare aqui cuando necesites de mis servicios.";
		close;
	}
	end;
}

-	script	Maroll Battle Recruiter::BatRecruit	728,{
	mes "[Maroll Battle Recruiter]";
	mes "Buen dia, Aventurero.";
	mes "Soy un Caballero de un reino muy lejano llamada Maroll.";
	next;
	mes "[Maroll Battle Recruiter]";
	mes "Los dos principes del reino estan luchando por el trono de Maroll, y necesitan de soldados experimentados como tu.";
	mes "¿Cómo le gustaria a prestar su poder a uno de los principes en Reino de Maroll?";
	next;
	switch(select("Unirme:No unirme")) {
	case 1:
		mes "[Maroll Battle Recruiter]";
		mes "Que el dios de la guerra te bendiga.";
		close2;
		getmapxy(.@mapname$,.@x,.@y,1);
		if (.@mapname$ == "prontera")
			set bat_return,1;
		else if (.@mapname$ == "moc_ruins")
			set bat_return,2;
		else if (.@mapname$ == "aldebaran")
			set bat_return,3;
		else if (.@mapname$ == "geffen")
			set bat_return,4;
		else if (.@mapname$ == "payon")
			set bat_return,5;
		else if (.@mapname$ == "lighthalzen")
			set bat_return,6;
		else if (.@mapname$ == "rachel")
			set bat_return,7;
		else
			set bat_return,1;
		warp "bat_room",154,150;
		break;
	case 2:
		mes "[Maroll Battle Recruiter]";
		mes "Siempre voy a estar estacionado aquí por más soldados. Siéntase libre de volver cuando le interese.";
		close;
	}
	end;
}

prontera,123,83,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit1	728
moc_ruins,75,162,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit2	728
aldebaran,146,109,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit3	728
geffen,109,66,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit4	728
payon,189,105,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit5	728
lighthalzen,153,86,5	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit6	728
rachel,149,138,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit7	728

// Additional warps
// Empty
bat_room,57,81,0	warp	bat1	1,1,bat_room,154,149
bat_room,57,90,0	warp	bat2	1,1,bat_room,154,149
// Empty
bat_room,85,81,0	warp	bat5	1,1,bat_room,154,149
bat_room,85,90,0	warp	bat6	1,1,bat_room,154,149
// Free BG
bat_room,113,81,0	warp	bat9	1,1,bat_room,154,149
bat_room,113,90,0	warp	bat10	1,1,bat_room,154,149
// Free BG
bat_room,141,81,0	warp	bat13	1,1,bat_room,154,149
bat_room,141,90,0	warp	bat14	1,1,bat_room,154,149
// Free BG
bat_room,169,81,0	warp	bat17	1,1,bat_room,154,149
bat_room,169,90,0	warp	bat18	1,1,bat_room,154,149
// Free BG
bat_room,197,81,0	warp	bat21	1,1,bat_room,154,149
bat_room,197,90,0	warp	bat22	1,1,bat_room,154,149
// Free BG
bat_room,225,81,0	warp	bat25	1,1,bat_room,154,149
bat_room,225,90,0	warp	bat26	1,1,bat_room,154,149
// Empty
bat_room,253,81,0	warp	bat29	1,1,bat_room,154,149
bat_room,253,90,0	warp	bat30	1,1,bat_room,154,149
// Empty
bat_room,253,220,0	warp	bat31	1,1,bat_room,154,149
bat_room,253,211,0	warp	bat32	1,1,bat_room,154,149

// Kafra
//============================================================
bat_room,148,147,4	script	Kafra Staff::kaf_bat	861,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}

// Repairman
//============================================================
bat_room,138,144,4	script	Repairman#bg	86,{
	callfunc "repairmain","Repairman";
	end;
}

/*
// GM Management NPC
//============================================================
bat_room,1,151,3	script	Switch#batgnd	81,{
	input .@input,0,2000;
	if (.@input == 0) {
		mes "La orden se ha cancelado.";
		close;
	}
	else if (.@input == 1854) {
		mes "¿Puedo ayudarle?";
		next;
		switch(select("Close Battlefield:Open Battlefield:Reset a01:Reset b01:Reset a02:Reset b02")) {
		case 1:
			disablenpc "Tierra Gorge Officer#01a";
			disablenpc "Tierra Gorge Officer#02a";
			disablenpc "Tierra Gorge Officer#01b";
			disablenpc "Tierra Gorge Officer#02b";
			disablenpc "Flavius Officer#01a";
			disablenpc "Flavius Officer#01b";
			disablenpc "Flavius Officer#02a";
			disablenpc "Flavius Officer#02b";
			break;
		case 2:
			enablenpc "Tierra Gorge Officer#01a";
			enablenpc "Tierra Gorge Officer#02a";
			enablenpc "Tierra Gorge Officer#01b";
			enablenpc "Tierra Gorge Officer#02b";
			enablenpc "Flavius Officer#01a";
			enablenpc "Flavius Officer#01b";
			enablenpc "Flavius Officer#02a";
			enablenpc "Flavius Officer#02b";
			break;
		case 3:
			donpcevent "start#bat_a01::OnEnable";
			break;
		case 4:
			donpcevent "start#bat_b01::OnEnable";
			break;
		case 5:
			donpcevent "start#bat_a02::OnEnable";
			break;
		case 6:
			donpcevent "start#bat_b02::OnEnable";
			break;
		}
		mes "Completo";
		close;
	}
}
*/

// Badges Exchange
//============================================================
bat_room,160,150,3	script	Erundek	109,{
	if (checkweight(1201,1) == 0) {
		mes "- Espera un minuto !! -";
		mes "- Actualmente se está llevando -";
		mes "- demaciados items contigo. -";
		mes "- Por favor, intentelo otra vez -";
		mes "- despues de guardar algunas cosas en su almacen. -";
		close;
	}
	mes "[Erundek]";
	mes "¿Tienes las Battlefild Badges?";
	mes "Te puedo cambiar las Bravery Badges y las Valor Badges por items en recompensa.";
	next;
	switch(select("Cambiar medallas:Revisar el catalogo")) {
	case 1:
		mes "[Erundek]";
		mes "¿Cuales items te gustaria cambiar?";
		mes "Para mas informacion acerca de los items en recompensa, por favor revias el ^3131FFCatalogo^000000.";
		next;
		switch(select("Weapon:Armor:Accessory:Consumable")) {
		case 1:
			mes "[Erundek]";
			mes "Haz escojido ^3131FFArma^000000.";
			mes "Las siguientes armas pueden ser cambiadas por battlefield badges.";
			mes "Por favor sea consiente de que las ^3131FFBravery Badges son idicadas como (BB)^000000, y las ^3131FFValor Badges como (VB)^000000.";
			next;
			switch(select("Dagger/OneSword/TwoSword/TwoSpear:Staff/Mace/TwoAxe/Shuriken:Bow/Katar/Music/Whip:Book/Knuckle:Revolver/Rifle/Gatling/Shotgun/Launcher")) {
			case 1:
				mes "[Erundek]";
				mes "Los siguientes items estan disponibles en las siguientes categorias ^3131FFDagas, Espadas de 1 mano, Espadas de 2 manos, y las Lanzas de 2 manos^000000.";
				next;
				setarray .@Weapons[0],13036,7828,13037,7829,13411,7828,13410,7829,1183,7828,1184,7829,1425,7828,1482,7829;
				break;
			case 2:
				mes "[Erundek]";
				mes "Los siguientes items estan disponibles en las siguientes categorias  ^3131FFBaculos / Mazos / Hachas de 2 manos / Huuma Shuriken^000000";
				next;
				setarray .@Weapons[0],1632,7828,1633,7829,1634,7828,1635,7829,1543,7828,1542,7829,1380,7828,1379,7829,13305,7828,13306,7829;
				break;
			case 3:
				mes "[Erundek]";
				mes "Los siguientes items estan disponibles en las siguientes categorias ^3131FFArcos / Katar / Instrumentos Musicales / Latigos^000000.";
				next;
				setarray .@Weapons[0],1739,7828,1738,7829,1279,7828,1280,7829,1924,7828,1923,7829,1978,7828,1977,7829;
				break;
			case 4:
				mes "[Erundek]";
				mes "Los siguientes items estan disponibles en las siguientes categorias ^3131FFLibros / Empunaduras^000000.";
				next;
				setarray .@Weapons[0],1574,7828,1575,7829,1824,7828,1823,7829;
				break;
			case 5:
				mes "[Erundek]";
				mes "Los siguientes items estan disponibles en las siguientes categorias ^3131FFRevolver / Rifle / Metrallas / Escopetas / Lanzador de Granadas^000000.";
				next;
				setarray .@Weapons[0],13108,7828,13171,7829,13172,7828,13173,7829,13174,7829;
				break;
			}
			set .@menu$,"";
			for(set .@i,0; .@i<getarraysize(.@Weapons); set .@i,.@i+2)
				set .@menu$, .@menu$+getitemname(.@Weapons[.@i])+((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)")+":";
			set .@i, (select(.@menu$)-1)*2;
			set .@type$, ((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)");
			mes "[Erundek]";
			mes "Haz escojido ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000.";
			mes "Tu puedes cambiar este item con ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000.";
			mes "¿Te gustaria cambiar?";
			next;
			switch(select("No deseo cambiar:Cambiar")) {
			case 1:
				break;
			case 2:
				mes "[Erundek]";
				mes "Te gustaria gastar ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000 y recibir ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000?";
				next;
				mes "[Erundek]";
				mes "Recuerda, las recompensas de Battleground son Items ^FF0000Intrasferible entre usuarios^000000. ¿Seguro que quieres este item?";
				next;
				switch(select("Yes:No")) {
				case 1:
					mes "[Erundek]";
					if (countitem(.@Weapons[.@i+1]) >= 100) {
						mes "Gracias por el cambio.";
						delitem .@Weapons[.@i+1],100;
						getitem .@Weapons[.@i],1;
					}
					else mes "Lo siento, pero no tienes las Badges para hacer el cambio.";
					close;
				case 2:
					break;
				}
				break;
			}
			mes "[Erundek]";
			mes "¿Necesita más tiempo para revisar los items?";
			close;
		case 2:
			mes "[Erundek]";
			mes "Haz escojido ^3131FFArmadura^000000.";
			mes "Las siguientes Armaduras estan disponibles para el cambio por battlefield badges.";
			next;
			switch(select("Garments / Shoes:Armor")) {
			case 1:
				setarray .@Items[0],2538,50,2539,50,2540,50,2435,50,2436,50,2437,50;
				break;
			case 2:
				setarray .@Items[0],2376,80,2377,80,2378,80,2379,80,2380,80,2381,80,2382,80;
				break;
			}
			break;
		case 3:
			mes "[Erundek]";
			mes "Haz escojido ^3131FFAccesorio^000000.";
			mes "Tu puedes cambiar las Medal of Honors con tus Badges deacuerdo a las clase que tienes:";
			next;
			setarray .@Items[0],2733,500,2720,500,2721,500,2722,500,2723,500,2724,500,2725,500;
			set .@menu1$,"Gunslinger:Swordman/Taekwon Master:Thief:Acolyte:Magician:Archer:Merchant";
			break;
		case 4:
			mes "[Erundek]";
			mes "Haz elegido ^3131FFConsumible^000000.";
			mes "Los siguientes consumibles estan disponibles para cambiar por battlefield badges:";
			next;
			setarray .@Items[0],12269,10,12270,10,12271,5,12272,10,12273,10;
			break;
		}
		break;
	case 2:
		mes "[Erundek]";
		mes "Contamos con muchos items, asi que por favor heche un vistazo y compre con precaucion.";
		close2;
		ReadBook 11010,1;
		end;
	}
	set .@menu$,"";
	if (.@menu1$ != "") set .@menu$, .@menu1$;
	else for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+2)
		set .@menu$, .@menu$+getitemname(.@Items[.@i])+":";
	set .@i, (select(.@menu$)-1)*2;
	mes "[Erundek]";
	mes "Haz escojido ^3131FF"+getitemname(.@Items[.@i])+"^000000.";
	switch(.@Items[.@i]) {
		case 2720: mes "Este items es solo para las clases de Swordman y los Taekwon Master solamente."; break;
		case 2721: mes "Este item es solo para la clase Thief."; break;
		case 2722: mes "Este item es solo para la clase Acolyto."; break;
		case 2723: mes "Este item es solo para la clase Magician."; break;
		case 2724: mes "Este item es solo para la clase Archer."; break;
		case 2725: mes "Este item es solo para la clase Merchant."; break;
		case 2733: mes "Este item es solo para la clase Gunslinger."; break;
		default: break;
	}
	mes "Tu puedes cambiar este item con ^FF0000"+.@Items[.@i+1]+" "+getitemname(7828)+"o"+.@Items[.@i+1]+" "+getitemname(7829)+"^000000.";
	mes "¿Te gustaria cambiar?";
	next;
	switch(select("No deseo cambiar:Cambiar")) {
	case 1:
		mes "[Erundek]";
		mes "¿Necesitas mas tiempo para revisar los items?";
		break;
	case 2:
		mes "[Erundek]";
		mes "¿Que Badge quieres cambiar?";
		mes "Necesitas ^3131FF"+.@Items[.@i+1]+" Badges^000000 para cambiar.";
		next;
		if (.@item[0] < 12269 || .@item[0] > 12273 ) {
			mes "[Erundek]";
			mes "Recuerda, las recompensas de Battleground son Items ^FF0000Intrasferible entre usuarios^000000. ¿Seguro que quieres este item?";
			next;
		}
		set .@j, select("Bravery Badge:Valor Badge:Cancel");
		mes "[Erundek]";
		if (.@j == 3) {
			mes "Haz cancelado el cambio.";
			break;
		}
		set .@cost, ((.@j==1)?7828:7829);
		if (countitem(.@cost) >= .@Items[.@i+1]) {
			mes "Gracias por el cambio.";
			delitem .@cost, .@Items[.@i+1];
			getitem .@Items[.@i],1;
		}
		else mes "No tienes sufientes "+getitemname(.@cost)+"s.";
		break;
	}
	close;
}

